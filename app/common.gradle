gradle.allprojects {
    // 主版本号
    def major = 0
    // 次版本号
    def minor = 0

    ext.getGitHeadRefsSuffix = {
        try {
            // .git/HEAD描述当前目录所指向的分支信息，内容示例："ref: refs/heads/master\n"
            def headFile = new File('.git/HEAD')
            if (headFile.exists()) {
                String[] strings = headFile.getText('UTF-8').split(" ")
                if (strings.size() > 1) {
                    String refFilePath = '.git/' + strings[1];
                    // 根据HEAD读取当前指向的hash值，路径示例为：".git/refs/heads/master"
                    def refFile = new File(refFilePath.replace("\n", ""));
                    // 索引文件内容为hash值+"\n"，
                    // 示例："90312cd9157587d11779ed7be776e3220050b308\n"
                    return refFile.getText('UTF-8').substring(0, 7)
                }

            }
        } catch (Exception e) {
            e.printStackTrace()
        }
        return ""
    }

    ext.getVersionC = {
        try {
            def cmd = 'git rev-list HEAD --count'
            def getVersionC = cmd.execute().text.trim().toInteger()
            return getVersionC
        } catch (Exception e) {
            e.printStackTrace()
        }
        return ""
    }

    ext.getVersionN = {
        // <APP名称><主版本号>.<子版本号><SVN最后提交数>
        return major + "." + minor + "_" + getVersionC() + "_" + getGitHeadRefsSuffix()
    }
}